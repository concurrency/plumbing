--** occam bindings for the Wiring API.
-- @module wiring

#IF NOT (DEFINED (WIRING.MODULE))
#DEFINE WIRING.MODULE

#INCLUDE "avr.module"

--{{{  constants
--* Symbolic constant for digital LOW on an Arduino pin.
VAL INT LOW IS #0:
--* Symbolic constant for digital HIGH on an Arduino pin.
VAL INT HIGH IS #1:

VAL INT INPUT IS #0:
VAL INT OUTPUT IS #1:

VAL INT SERIAL  IS #0:
VAL INT DISPLAY IS #1:

VAL INT LSBFIRST IS 0:
VAL INT MSBFIRST IS 1:

VAL INT CHANGE IS 1:
VAL INT FALLING IS 2:
VAL INT RISING IS 3:

VAL INT INTERNAL IS 3:
VAL INT DEFAULT IS 1:
VAL INT EXTERNAL IS 0:


-- Mappings for pin.location
VAL []INT DDR.MAP IS
  [DDRD, DDRD, DDRD, DDRD, DDRD, DDRD, DDRD, DDRD,
   DDRB, DDRB, DDRB, DDRB, DDRB, DDRB,
   DDRC, DDRC, DDRC, DDRC, DDRC, DDRC]:
VAL []INT PORT.MAP IS
  [PORTD, PORTD, PORTD, PORTD, PORTD, PORTD, PORTD, PORTD,
   PORTB, PORTB, PORTB, PORTB, PORTB, PORTB,
   PORTC, PORTC, PORTC, PORTC, PORTC, PORTC]:
VAL []INT PIN.MAP IS
  [PIND, PIND, PIND, PIND, PIND, PIND, PIND, PIND,
   PINB, PINB, PINB, PINB, PINB, PINB,
   PINC, PINC, PINC, PINC, PINC, PINC]:
--}}}

--{{{  PROC delay
--* Wait for a period of time.
-- @param millis Time in wait, in milliseconds
PROC delay (VAL INT millis)
  TIMER tim:
  INT t:
  SEQ
    tim ? t
    tim ? AFTER t PLUS millis
:
--}}}

--{{{ PROC die
--* Kills everything and reports an error back to the user.
-- @param error A string containing the error message.
INLINE PROC die (VAL []BYTE error)
  SEQ
    --{{{  force error to be used
    INT n:
    n := SIZE error
    --}}}
    CAUSEERROR ()
:
--}}}

--{{{  FUNCTION pin.location
--* Map an Arduino pin number to a DDR register, PORT register, PIN register
-- and bit number.
--
-- On the Arduino NG, Diecimilia, Duemilanove and similar boards:
--
-- [@item] digital pins 0-7 map to [@ref PORTD] bits 0-7
-- [@item] digital pins 8-13 map to [@ref PORTB] bits 0-5
-- [@item] analog pins 0-5 are also known as digital pins 14-19, and map to
--   [@ref PORTC] bits 0-5
--
-- A request for a pin out of range will result in an unrecoverable error.
--
-- @param pin Arduino pin number, as marked on the board
-- @returns ddr.reg The [@text DDR] register
-- @returns port.reg The [@text PORT] register
-- @returns pin.reg The [@text PIN] register
-- @returns bit The bit number in all the above registers
INT, INT, INT, INT INLINE FUNCTION pin.location (VAL INT pin)
  INT bit:
  VALOF
    IF
      (pin < 0) OR (pin > 19)
        die ("requested pin number is not valid (must be between 0 and 19)")
      pin < 8
        bit := pin
      pin < 14
        bit := pin - 8
      TRUE
        bit := pin - 14
    RESULT DDR.MAP[pin], PORT.MAP[pin], PIN.MAP[pin], bit
:
--}}}

--{{{  PROC digitalWrite
--* Sets the state of a pin to either HIGH or LOW.
-- @param pin The Arduino pin number.
-- @param state Either HIGH or LOW.
PROC digitalWrite (VAL INT pin, state)
  INT r.ddr, r.port, r.pin, bit:
  PLACED [MAX.PORT]BYTE ports 0:
  #PRAGMA DEFINED ports
  SEQ
    r.ddr, r.port, r.pin, bit := pin.location (pin)
    ports[r.port] := (ports[r.port] /\ (~BV (bit))) \/ ((BYTE state) << bit)
:
--}}}
--{{{  PROC digitalRead
--* Read the value of a digital pin. Either HIGH or LOW.
-- @param pin The Arduino pin number.
-- @param state The state of the pin will be stored in this variable.
PROC digitalRead (VAL INT pin, RESULT INT state)
  INT r.ddr, r.port, r.pin, bit:
  PLACED [MAX.PORT]BYTE ports 0:
  #PRAGMA DEFINED ports
  SEQ
    r.ddr, r.port, r.pin, bit := pin.location (pin)
    state := (INT ((ports[r.pin] /\ (BV (bit))) >> bit))
:
--}}}


--{{{  PROC pinMode
PROC pinMode (VAL INT pin, mode)
  INT r.ddr, r.port, r.pin, bit:
  PLACED [MAX.PORT]BYTE ports 0:
  #PRAGMA DEFINED ports
  SEQ
    r.ddr, r.port, r.pin, bit := pin.location (pin)
    ports[r.ddr] := (ports[r.ddr] /\ (~BV (bit))) \/ ((BYTE mode) << bit)
:
--}}}

--{{{  PROC beginSerial
--* Initialise the serial port.
-- @param baud Baud rate
PROC beginSerial (VAL INT32 baud)
  VAL INT32 factor IS (((F.CPU / 16) + (baud / 2)) / baud) - 1:
  VAL [4]BYTE factor.bytes RETYPES factor:

  PLACED [MAX.PORT]BYTE ports 0:
  SEQ
    ports[UBRR0H] := factor.bytes[1]
    ports[UBRR0L] := factor.bytes[0]

    ports[UCSR0C] := 3 << UCSZ00
    ports[UCSR0B] := BV (TXEN0)
:
--}}}
--{{{  PROC serialWrite
--* Write a string to the serial port.
-- @param string Bytes to write
PROC serialWrite (VAL []BYTE string)
  SEQ i = 0 FOR SIZE string
    PLACED [MAX.PORT]BYTE ports 0:
    #PRAGMA DEFINED ports
    SEQ
      WHILE (ports[UCSR0A] /\ BV (UDRE0)) = 0
        SKIP
      ports[UDR0] := string[i]
:
--}}}
--{{{  PROC serialWriteInt
--* Write an [@code INT] to the serial port in hex.
-- @param n The integer to write.
PROC serialWriteInt (VAL INT n)
  VAL []BYTE digits IS "0123456789abcdef":
  SEQ i = 3 FOR 4 STEP -1
    serialWrite ([digits[(n >> (i * 4)) /\ #F]])
:
--}}}
--{{{  PROC serialWriteInt32
--* Write an [@code INT32] to the serial port in hex.
-- @param n The long integer ([@code INT32]) to write.
PROC serialWriteInt32 (VAL INT32 n)
  VAL []BYTE digits IS "0123456789abcdef":
  SEQ i = 7 FOR 8 STEP -1
    serialWrite ([digits[INT ((n >> (i * 4)) /\ #F)]])
:
--}}}

VAL INT NO.MAP IS (- 1):
VAL INT INDEX.OFFSET IS 3:
VAL []INT ARDUINO.PIN.MAP IS [5, NO.MAP, 1, 0, NO.MAP, NO.MAP, 2, 3, 4]:
VAL []INT TCR.MODE IS [TCCR0A, TCCR0A, TCCR1A, TCCR1A, TCCR2A, TCCR2A]:
VAL []INT TCR.PRESCALE IS [TCCR0B, TCCR0B, TCCR1B, TCCR1B, TCCR2B, TCCR2B]:
VAL []INT CS1 IS [CS01, CS01, CS11, CS11, CS22, CS22]:
VAL []INT CS0 IS [CS00, CS00, CS10, CS10, CS22, CS22]:
VAL []INT COM IS [COM0A1, COM0B1, COM1A1, COM1B1, COM2A1, COM2B1]:
VAL []INT WGM IS [WGM00, WGM00, WGM10, WGM10, WGM20, WGM20]:
VAL []INT OCR IS [OCR0A, OCR0B, OCR1A, OCR1B, OCR2A, OCR2B]:

--{{{  PROC beginAnalog
--* Initialize analog input.
-- Only works on PWM pins. 
-- @param pin The Arduino pin number.
PROC beginAnalog (VAL INT pin)
  PLACED [MAX.PORT]BYTE ports 0:
  #PRAGMA DEFINED ports
  INITIAL INT index IS ARDUINO.PIN.MAP[pin - INDEX.OFFSET]:
  SEQ
    pinMode(pin, OUTPUT) 
    ports[TCR.MODE[index]] := ports[TCR.MODE[index]] \/ (BV (COM[index]) \/ BV (WGM[index]))
    -- Set prescale to 64... don't change that. The VM is using that
    -- for TCCR2, and we'll (for the moment) use the same everywhere.
    ports[TCR.PRESCALE[index]] := BV(CS1[index]) \/ BV(CS0[index])
    ports[OCR[index]] := 0
:
--}}}


--{{{  PROC analogWrite
--* Write an analog value to a pin.
-- @param pin The Arduino pin number.
-- @param level A [@code BYTE] representing the analog level (0 -> 255)
PROC analogWrite (VAL INT pin, VAL BYTE level)
  PLACED [MAX.PORT]BYTE ports 0:
  #PRAGMA DEFINED ports
  INITIAL INT index IS ARDUINO.PIN.MAP[pin - INDEX.OFFSET]:
  SEQ
    ports[OCR[index]] := level
:
--}}}


--{{{  PROC endAnalog
--* Disable analog output on Arduino.
-- Only works on PWM pins.
-- @param pin The Arduino pin number.
PROC endAnalog (VAL INT pin)
  PLACED [MAX.PORT]BYTE ports 0:
  #PRAGMA DEFINED ports
  INITIAL INT index IS ARDUINO.PIN.MAP[pin - INDEX.OFFSET]:
  SEQ
    ports[TCR.MODE[index]] := ports[TCR.MODE[index]] /\ (~ BV(COM[index]))
:
--}}}

#ENDIF
