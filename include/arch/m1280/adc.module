--** ACD routines for the 1280
-- @module adc

#IF NOT (DEFINED (ADC.MODULE))
#DEFINE ADC.MODULE

PROC adc.base (VAL INT pin, VAL BOOL USE.VCC, RESULT INT v)
  PLACED [MAX.PORT]BYTE ports 0:
  INT16 result:
  SEQ
    -- Enable ADC; enable interrupt; clock prescale 128
    ports[ADCSRA] := (BV (ADEN) \/ BV (ADIE)) \/ (7 << ADPS0)
    -- FIXME: ADPS0 is.. 0. Why are we filling the register with 1s?
    -- Disable digital inputs on all 16 ADC pins.
    -- FIXME: Seems a bit much? Perhaps we should pinpoint which pin to switch?
    ports[DIDR0] := #3F
    ports[DIDR2] := #3F

    -- Select the channel
    IF
      pin < 8
        ports[ADMUX] := (BYTE pin)
      TRUE
        SEQ
          ports[ADMUX] := (BYTE (pin - 8))
          ports[ADCSRB] := BV (MUX5)
    -- If we are using Vcc as out reference, set bit REFS0
    IF
      USE.VCC
        ports[ADMUX] := ports[ADMUX] \/ BV (REFS0)
      TRUE
        SKIP

    -- Start conversion
    ports[ADCSRA] := ports[ADCSRA] \/ BV (ADSC)
    WHILE ((ports[ADCSRA] /\ BV(ADSC)) = BV (ADSC))
      SKIP

    -- Read value
    -- (We can't do this by retyping ports, since that does a 16-bit read)
    [2]BYTE bytes RETYPES result:
    bytes[0], bytes[1] := ports[ADCL], ports[ADCH]

    v := (INT result)
:

INLINE PROC adc.aref (VAL INT pin, RESULT INT v)
  adc.base (pin, TRUE, v)
:

INLINE PROC adc.vcc (VAL INT pin, RESULT INT v)
  adc.base (pin, FALSE, v)
:

#ENDIF
