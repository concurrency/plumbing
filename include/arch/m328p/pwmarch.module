--** Pulse Width Modulation board-specific helper functions for the ATmega328.
-- @module 328.pwmarch


#IF NOT (DEFINED (PWMARCH.MODULE))
#DEFINE PWMARCH.MODULE

--{{{ FUNCTION pwm8.lookup
--* Lookup function that returns a variety of useful numbers invloved with
-- the operation of Puse Width Modulation on 8 bit timers.
-- @param avr.pin The [@em AVR] pin connected to the PWM operations.
-- @returns TCCRnA Timer/Counter Control Register A for the given PWM pin.
-- @returns TCCRnB Timer/Counter Control Register B for the given PWM pin.
-- @returns OCRnx Output Compare Register for the given PWM pin.
-- @returns TCCRnA.bit The BYTE to be written into TCCRnA. Might be modified
--   by aditional FUNCTIONs.
-- @returns TCCRnB.bit The BYTE to be written into TCCRnB. Might be modified
--   by aditional FUNCTIONs.
INT, INT, INT, BYTE, BYTE FUNCTION pwm8.lookup (VAL INT avr.pin)
  INT TCCRnA, TCCRnB, OCRnx:
  BYTE TCCRnA.bits, TCCRnB.bits:
  VALOF
    SEQ
      TCCRnB.bits := 0
      CASE avr.pin
        5
          SEQ
            TCCRnA := TCCR2A
            TCCRnB := TCCR2B
            TCCRnA.bits := (BV (WGM20) \/ BV (COM2B1))
            OCRnx := OCR2B
        11
          SEQ
            TCCRnA := TCCR0A
            TCCRnB := TCCR0B
            TCCRnA.bits := (BV (WGM00) \/ BV (COM0B1))
            OCRnx := OCR0B
        12
          SEQ
            TCCRnA := TCCR0A
            TCCRnB := TCCR0B
            TCCRnA.bits := (BV (WGM00) \/ BV (COM0A1))
            OCRnx := OCR0A
        15
          SEQ
            TCCRnA := TCCR1A
            TCCRnB := TCCR1B
            TCCRnA.bits := (BV (WGM10) \/ BV (COM1A1))
            OCRnx := OCR1A
        16
          SEQ
            TCCRnA := TCCR1A
            TCCRnB := TCCR1B
            TCCRnA.bits := (BV (WGM10) \/ BV (COM1B1))
            OCRnx := OCR1B
        17
          SEQ
            TCCRnA := TCCR2A
            TCCRnB := TCCR2B
            TCCRnA.bits := (BV (WGM20) \/ BV (COM2A1))
            OCRnx := OCR2A
        ELSE
          die ("The Arduino only supports 8-bit PWM on pins 3,5,6,9,10 and 11.")
    RESULT TCCRnA, TCCRnB, OCRnx, TCCRnA.bits, TCCRnB.bits
:
--}}}

--{{{ FUNCTION pwm16.lookup
--* Lookup function that returns a variety of useful numbers invloved with
-- the operation of Puse Width Modulation on 16 bit timers.
-- @param avr.pin The [@em AVR] pin connected to the PWM operations.
-- @returns TCCRnA Timer/Counter Control Register A for the given PWM pin.
-- @returns TCCRnB Timer/Counter Control Register B for the given PWM pin.
-- @returns OCRnx Output Compare Register for the given PWM pin.
-- @returns TCCRnA.bit The BYTE to be written into TCCRnA. Might be modified
--   by aditional FUNCTIONs.
-- @returns TCCRnB.bit The BYTE to be written into TCCRnB. Might be modified
--   by aditional FUNCTIONs.
INT, INT, INT, BYTE, BYTE FUNCTION pwm16.lookup (VAL INT avr.pin)
  INT TCCRnA, TCCRnB, OCRnx:
  BYTE TCCRnA.bits, TCCRnB.bits:
  VALOF
    SEQ
      TCCRnA := TCCR1A
      TCCRnB := TCCR1B
      TCCRnB.bits := BV (WGM13)
      CASE avr.pin
        15
          SEQ
            TCCRnA.bits := (BV (COM1A1))
            OCRnx := OCR1A
        16
          SEQ
            TCCRnA.bits := (BV (COM1B1))
            OCRnx := OCR1B
        ELSE
          die ("The Arduino only supports 16-bit PWM on pins 9 and 10.")
    RESULT TCCRnA, TCCRnB, OCRnx, TCCRnA.bits, TCCRnB.bits
:
--}}}

--{{{ FUNCTION pwm8.TCCRnB.helper
--* Helper function that modifies the TCCRnB.bits BYTE initially found in
-- pwm8.lookup based on the desired prescale value to be used with in the
-- PWM operations.
-- @param TCCRnB.bits The TCCRnB.bits BYTE initially found in pwm8.lookup.
-- @param prescale The desired prescale value.
-- @returns TCCRnB.bits The modified TCCRnB.bits BYTE to
--   be written into TCCRnB.
BYTE FUNCTION pwm8.TCCRnB.helper (VAL BYTE TCCRnB.bits, VAL INT prescale)
  BYTE bits:
  VALOF
    SEQ
      CASE prescale
        0
          bits := TCCRnB.bits \/ #01 -- := BV (CSn0)
        8
          bits := TCCRnB.bits \/ #02 -- := BV (CSn1)
        64
          bits := TCCRnB.bits \/ #03 -- := BV (CSn0) \/ BV (CSn1)
        256
          bits := TCCRnB.bits \/ #04 -- := BV (CSn2)
        1024
          bits := TCCRnB.bits \/ #05 -- := BV (CSn2) \/ BV (CSn0)
        ELSE
          die ("Invalid clock prescale value")
    RESULT bits
:
--}}}

--{{{ FUNCTION pwm16.TCCRnB.helper
--* Helper function that modifies the TCCRnB.bits BYTE initially found in
-- pwm16.lookup based on the desired prescale value to be used with in the
-- PWM operations.
-- @param TCCRnB.bits The TCCRnB.bits BYTE initially found in pwm16.lookup.
-- @param prescale The desired prescale value.
-- @returns TCCRnB.bits The modified TCCRnB.bits BYTE to
--   be written into TCCRnB.
BYTE FUNCTION pwm16.TCCRnB.helper (VAL BYTE TCCRnB.bits, VAL INT prescale)
  BYTE bits:
  VALOF
    CASE prescale
      0
        bits := TCCRnB.bits \/ #01 -- := BV (CSn0)
      8
        bits := TCCRnB.bits \/ #02 -- := BV (CSn1)
      64
        bits := TCCRnB.bits \/ #03 -- := BV (CSn0) \/ BV (CSn1)
      256
        bits := TCCRnB.bits \/ #04 -- := BV (CSn2)
      1024
        bits := TCCRnB.bits \/ #05 -- := BV (CSn2) \/ BV (CSn0)
      ELSE
        die ("Invalid clock prescale value")
    RESULT bits
:
--}}}

--{{{ FUNCTION pwm8.compare.lookup
--* Lookup FUNCTION for finding only the Output Compare Register for the
-- given [@em AVR] pin being used for PWM operations.
-- @param avr.pin The given AVR pin being used for PWM operations.
-- @returns OCRnx Output Compare Register for the given PWM pin.
INT FUNCTION pwm8.compare.lookup (VAL INT avr.pin)
  INT OCRnx:
  VALOF
    CASE avr.pin
      5
        OCRnx := OCR2B
      11
        OCRnx := OCR0B
      12
        OCRnx := OCR0A
      15
        OCRnx := OCR1A
      16
        OCRnx := OCR1B
      17
        OCRnx := OCR2A
      ELSE
        die ("The Arduino only supports 8-bit PWM on pins 3,5,6,9,10 and 11.")
    RESULT OCRnx
:
--}}}

--{{{ FUNCTION pwm16.compare.lookup
--* Lookup FUNCTION for finding only the Output Compare Register for the
-- given [@em AVR] pin being used for PWM operations.
-- @param avr.pin The given AVR pin being used for PWM operations.
-- @returns OCRnx Output Compare Register for the given PWM pin.
INT FUNCTION pwm16.compare.lookup (VAL INT avr.pin)
  INT OCRnx:
  VALOF
    CASE avr.pin
      15
        OCRnx := OCR1A
      16
        OCRnx := OCR1B
      ELSE
        die ("The Arduino only supports 16-bit PWM on pins 9 and 10.")
    RESULT OCRnx
:
--}}}

--{{{ FUNCTION pwm16.ICRn.lookup
--* Looup FUNCTION for finding only the Input Capture Register for the given
-- [@em 16bit], [@em AVR] pin being used for PWM operations.
-- @param avr.pin The given AVR pin being used for PWM operations.
-- @returns ICRn The Input Capture Register for the given PWM pin.
INT FUNCTION pwm16.ICRn.lookup (VAL INT avr.pin)
  INT ICRn:
  VALOF
    CASE avr.pin
      15, 16
        ICRn := ICR1
      ELSE
        die ("The Arduino only supports 16-bit PWM on pins 9 and 10.")
    RESULT ICRn
: 
--}}}

#ENDIF
