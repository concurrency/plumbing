--** ADC routines for the 328
-- @module adc

#IF NOT (DEFINED (ADC.MODULE))
#DEFINE ADC.MODULE

-- The AVR analog pins start at (AVR) pin 23.
-- This is used for calculating analog channel choice
-- in adc.module
VAL INT AVR.A0 IS 23:

PROC adc.base (VAL INT avr.pin, VAL BOOL USE.VCC, RESULT INT v)
  PLACED [MAX.PORT]BYTE ports 0:
  #PRAGMA DEFINED ports
  INT16 result:
  -- AVR pins for Analog start at pin 23.
  INITIAL INT a.pin IS avr.pin - AVR.A0:
  SEQ
    -- Enable ADC; enable interrupt; clock prescale 128
    -- The '7' is == ADPS2 \/ ADPS1 \/ ADPS0 which sest the prescale to 128
    ports[ADCSRA] := (BV (ADEN) \/ BV (ADIE)) \/ 7
    -- Disable digital inputs on the ADC pin.
    ports[DIDR0] := ports[DIDR0] \/ BV (a.pin)

    -- Select the channel
    -- If we are using Vcc as our reference, set bit REFS0.
    IF
      USE.VCC
        ports[ADMUX] := BV (REFS0) \/ (BYTE a.pin)
      TRUE
        ports[ADMUX] := (BYTE a.pin)

    -- Start conversion
    ports[ADCSRA] := ports[ADCSRA] \/ BV (ADSC)
    WHILE ((ports[ADCSRA] /\ BV(ADSC)) = BV (ADSC))
      SKIP

    -- Read value
    -- (We can't do this by retyping ports, since that does a 16-bit read)
    [2]BYTE bytes RETYPES result:
    bytes[0], bytes[1] := ports[ADCL], ports[ADCH]

    v := (INT result)
:

INLINE PROC adc.aref (VAL INT avr.pin, RESULT INT v)
  adc.base (avr.pin, FALSE, v)
:

INLINE PROC adc.vcc (VAL INT avr.pin, RESULT INT v)
  adc.base (avr.pin, TRUE, v)
:

#ENDIF
